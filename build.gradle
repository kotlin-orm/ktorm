
buildscript {
    ext {
        kotlinVersion = "1.4.32"
        detektVersion = "1.17.0-RC2"
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${detektVersion}"
    }
}

allprojects {
    group = "org.ktorm"
    version = "3.4.0-SNAPSHOT"
}

subprojects { project ->
    apply plugin: "kotlin"
    apply plugin: "signing"
    apply plugin: "maven-publish"
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply from: "${project.rootDir}/check-source-header.gradle"

    repositories {
        mavenCentral()
    }

    dependencies {
        api "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
        api "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
        testImplementation "junit:junit:4.12"
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${detektVersion}"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.6"
        kotlinOptions.allWarningsAsErrors = true
        kotlinOptions.freeCompilerArgs = [
            "-Xexplicit-api=strict",
            "-Xopt-in=kotlin.RequiresOptIn"
        ]
    }

    task generateSources(type: Jar) {
        archiveClassifier = "sources"
        from sourceSets.main.allSource
    }

    task generateJavadoc(type: Jar) {
        archiveClassifier = "javadoc"
    }

    detekt {
        toolVersion = detektVersion
        input = files("src/main/kotlin")
        config = files("${project.rootDir}/detekt.yml")
    }

    publishing {
        publications {
            dist(MavenPublication) {
                from components.java
                artifact generateSources
                artifact generateJavadoc

                groupId = project.group
                artifactId = project.name
                version = project.version

                pom {
                    name = "${project.group}:${project.name}"
                    description = "A lightweight ORM Framework for Kotlin with strong typed SQL DSL and sequence APIs."
                    url = "https://www.ktorm.org"
                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        url = "https://github.com/kotlin-orm/ktorm"
                        connection = "scm:git:https://github.com/kotlin-orm/ktorm.git"
                        developerConnection = "scm:git:ssh://git@github.com/kotlin-orm/ktorm.git"
                    }
                    developers {
                        developer {
                            id = "vincentlauvlwj"
                            name = "vince"
                            email = "me@liuwj.me"
                        }
                        developer {
                            id = "waluo"
                            name = "waluo"
                            email = "1b79349b@gmail.com"
                        }
                        developer {
                            id = "clydebarrow"
                            name = "Clyde"
                            email = "clyde@control-j.com"
                        }
                        developer {
                            id = "Ray-Eldath"
                            name = "Ray Eldath"
                            email = "ray.eldath@outlook.com"
                        }
                        developer {
                            id = "hangingman"
                            name = "hiroyuki.nagata"
                            email = "idiotpanzer@gmail.com"
                        }
                        developer {
                            id = "onXoot"
                            name = "beetlerx"
                            email = "beetlerx@gmail.com"
                        }
                        developer {
                            id = "arustleund"
                            name = "Andrew Rustleund"
                            email = "andrew@rustleund.com"
                        }
                        developer {
                            id = "afezeria"
                            name = "afezeria"
                            email = "zodal@outlook.com"
                        }
                        developer {
                            id = "scorsi"
                            name = "Sylvain Corsini"
                            email = "sylvain.corsini@protonmail.com"
                        }
                        developer {
                            id = "lyndsysimon"
                            name = "Lyndsy Simon"
                            email = "lyndsy@lyndsysimon.com"
                        }
                        developer {
                            id = "antonydenyer"
                            name = "Antony Denyer"
                            email = "git@antonydenyer.co.uk"
                        }
                        developer {
                            id = "mik629"
                            name = "Mikhail Erkhov"
                            email = "mikhail.erkhov@gmail.com"
                        }
                        developer {
                            id = "sinzed"
                            name = "Saeed Zahedi"
                            email = "saeedzhd@gmail.com"
                        }
                        developer {
                            id = "smn-dv"
                            name = "Simon Schoof"
                            email = "simon.schoof@hey.com"
                        }
                        developer {
                            id = "pedrod"
                            name = "Pedro Domingues"
                            email = "pedro.domingues.pt@gmail.com"
                        }
                        developer {
                            id = "efenderbosch"
                            name = "Eric Fenderbosch"
                            email = "eric@fender.net"
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = "central"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username = System.getenv("OSSRH_USER")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
            maven {
                name = "snapshot"
                url = "https://oss.sonatype.org/content/repositories/snapshots"
                credentials {
                    username = System.getenv("OSSRH_USER")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }

    signing {
        def keyId = System.getenv("GPG_KEY_ID")
        def secretKey = System.getenv("GPG_SECRET_KEY")
        def password = System.getenv("GPG_PASSWORD")

        required { !project.version.endsWith("SNAPSHOT") }
        useInMemoryPgpKeys(keyId, secretKey, password)
        sign publishing.publications.dist
    }
}

task printClasspath() {
    doLast {
        def jars = subprojects.collect { it.configurations.compileClasspath.getFiles() }.flatten().toSet()
        jars.removeIf { it.name.contains("ktorm") }

        println("Project classpath: ")
        jars.each { println(it.name) }

        def file = file("build/ktorm.classpath")
        file.parentFile.mkdirs()
        file.write(jars.collect { it.absolutePath }.join(File.pathSeparator))
        println("Classpath written to build/ktorm.classpath")
    }
}
